buildscript {
  repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/spinnaker/gradle/" }
  }
  dependencies {
    classpath("com.netflix.spinnaker.gradle:spinnaker-extensions:$spinnakerGradleVersion")
  }
}

plugins {
  id("com.palantir.git-version").version("0.12.2")
  id("com.moowork.node").version("1.3.1").apply(false)
}
apply plugin: "io.spinnaker.plugin.bundler"

spinnakerBundle {
  pluginId = "Armory.PulumiPlugin"
  description = "A Pulumi plugin that provides a pulumi pipeline stage."
  provider = "https://github.com/spinnaker-hackathon"
  version = rootProject.version
}

version = normalizedVersion()

subprojects {
  group = "io.armory.plugin.stage.pulumi"
  version = rootProject.version
}

String normalizedVersion() {
  String fullVersion = gitVersion()
  String normalized = fullVersion.split("-").first()
  if (fullVersion.contains("dirty")) {
    return "$normalized-SNAPSHOT"
  } else {
    return normalized
  }
}
